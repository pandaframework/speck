apply plugin: 'jacoco'
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:0.5.6.RELEASE"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.1"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.7"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.1.0"
    }
}

repositories {
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}

if (hasProperty("bintray_user")) {
    ext.bintrayUser = bintray_user
} else {
    ext.bintrayUser = ""
}

if (hasProperty("bintray_api_key")) {
    ext.bintrayApiKey = bintray_api_key
} else {
    ext.bintrayApiKey = ""
}

allprojects {
    group = "io.polymorphicpanda.kspec"
    version = "0.4.0-SNAPSHOT"
}

jacoco {
    toolVersion = "0.7.6.201602180812"
}


task jacocoTestReport(type: JacocoReport) {
    sourceDirectories = files()
    classDirectories = files()
    subprojects {
        afterEvaluate {
            if (plugins.hasPlugin(JavaPlugin)) {
                dependsOn tasks.getByName("check")
                executionData tasks.withType(Test)
                sourceDirectories += sourceSets.main.allSource
                classDirectories += sourceSets.main.output
            }
        }
    }

    reports {
        xml.enabled = true
    }
}

task check() {
    dependsOn jacocoTestReport
}
